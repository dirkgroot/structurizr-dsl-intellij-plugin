{
    parserClass="nl.dirkgroot.structurizr.dsl.StructurizrDSLParser"

    extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

    psiClassPrefix="SD"
    psiImplClassSuffix="Impl"
    psiPackage="nl.dirkgroot.structurizr.dsl.psi"
    psiImplPackage="nl.dirkgroot.structurizr.dsl.psi.impl"
    psiImplUtilClass="nl.dirkgroot.structurizr.dsl.psi.impl.SDPsiImplUtil"

    elementTypeHolderClass="nl.dirkgroot.structurizr.dsl.psi.SDTypes"
    elementTypeClass="nl.dirkgroot.structurizr.dsl.psi.SDElement"
    tokenTypeClass="nl.dirkgroot.structurizr.dsl.psi.SDToken"

    tokens=[
        line_comment='regexp://[^\r\n]*(\r|\n|\r\n)?'
        block_comment='regexp:/\*(.|\n)*\*/'

        crlf='crlf'
        brace1='{'
        brace2='}'
        equals='='

        quoted_text='regexp:"[^"\r\n]*"'
        unquoted_text='regexp:[^\s"\r\n]+'
    ]

    extends(".*Keyword")=keyword
    extends(".*Statement")=statement
}

structurizrDSLFile ::= statement*

statement ::= scriptStatement
            | explicitRelationshipStatement
            | implicitRelationshipStatement
            | animationStatement
            | propertiesStatement
            | identifierAssignmentStatement
            | blockStatement
            | singleLineStatement
            | lineCommentStatement
            | blockCommentStatement

explicitRelationshipStatement ::= relationshipSource relationshipKeyword relationshipDestination argument* block? lf_eof
implicitRelationshipStatement ::= relationshipKeyword relationshipDestination argument* block? lf_eof
relationshipSource ::= UNQUOTED_TEXT
relationshipKeyword ::= ARROW
relationshipDestination ::= UNQUOTED_TEXT

animationStatement ::= animationKeyword '{' CRLF (identifierReferencesStatement | lineCommentStatement | blockCommentStatement)* '}' lf_eof
animationKeyword ::= 'animation'
identifierReferencesStatement ::= identifierReference* CRLF

lineCommentStatement ::= LINE_COMMENT lf_eof
blockCommentStatement ::= BLOCK_COMMENT lf_eof

scriptStatement ::= scriptKeyword argument* scriptBlock lf_eof
scriptKeyword ::= '!script'
scriptBlock ::= '{' SCRIPT_TEXT '}'

propertiesStatement ::= propertiesKeyword propertiesBlock lf_eof
propertiesKeyword ::= 'perspectives' | 'properties' | 'users'
propertiesBlock ::= '{' CRLF propertyDefinition* '}'
propertyDefinition ::= propertyKey propertyValue CRLF
propertyKey ::= text
propertyValue ::= text

identifierAssignmentStatement ::= identifierReference '=' (blockStatement | singleLineStatement)
identifierReference ::= UNQUOTED_TEXT

blockStatement ::= keyword argument* block lf_eof
block ::= '{' CRLF statement* '}' {
    pin=1
    recoverWhile=blockRecover
}
private blockRecover ::= !(text | lf_eof)

singleLineStatement ::= keyword argument* lf_eof

keyword ::= UNQUOTED_TEXT
argument ::= text

private text ::= QUOTED_TEXT | UNQUOTED_TEXT
private lf_eof ::= CRLF | <<eof>>
