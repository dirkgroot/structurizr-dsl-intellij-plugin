{
    parserClass="nl.dirkgroot.structurizr.dsl.StructurizrDSLParser"

    extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

    psiClassPrefix="SD"
    psiImplClassSuffix="Impl"
    psiPackage="nl.dirkgroot.structurizr.dsl.psi"
    psiImplPackage="nl.dirkgroot.structurizr.dsl.psi.impl"
    psiImplUtilClass="nl.dirkgroot.structurizr.dsl.psi.impl.SDPsiImplUtil"

    elementTypeHolderClass="nl.dirkgroot.structurizr.dsl.psi.SDTypes"
    elementTypeClass="nl.dirkgroot.structurizr.dsl.psi.SDElement"
    tokenTypeClass="nl.dirkgroot.structurizr.dsl.psi.SDToken"

    tokens=[
        line_comment='regexp://[^\r\n]*(\r|\n|\r\n)?'
        block_comment='regexp:/\*(.|\n)*\*/'

        crlf='crlf'
        brace1='{'
        brace2='}'
        equals='='

        adrs_keyword='!adrs'
        animation_keyword='animation'
        auto_layout_keyword='autoLayout'
        background_keyword='background'
        border_keyword='border'
        branding_keyword='branding'
        color_keyword='color'
        colour_keyword='colour'
        component_keyword='component'
        configuration_keyword='configuration'
        constant_keyword='!constant'
        container_instance='containerInstance'
        container_keyword='container'
        custom_keyword='custom'
        deployment_environment_keyword='deploymentEnvironment'
        deployment_group_keyword='deploymentGroup'
        deployment_keyword='deployment'
        deployment_node_keyword='deploymentNode'
        description_keyword='description'
        docs_keyword='!docs'
        dynamic_keyword='dynamic'
        element_keyword='element'
        enterprise_keyword='enterprise'
        exclude_element_keyword='exclude'
        extends_keyword='extends'
        filtered_keyword='filtered'
        fontsize_keyword='fontSize'
        group_keyword='group'
        health_check_keyword='healthCheck'
        height_keyword='height'
        icon_keyword='icon'
        identifiers_keyword='!identifiers'
        implied_relationships_keyword='!impliedRelationships'
        include_element_keyword='include'
        include_file_keyword='!include'
        infrastructure_node_keyword='infrastructureNode'
        instances_keyword='instances'
        metadata_keyword='metadata'
        model_keyword='model'
        opacity_keyword='opacity'
        person_keyword='person'
        perspectives_keyword='perspectives'
        plugin_keyword='!plugin'
        position_keyword='position'
        properties_keyword='properties'
        ref_keyword='!ref'
        relationship_keyword='->'
        routing_keyword='routing'
        script_keyword='!script'
        shape_keyword='shape'
        software_system_instance_keyword='softwareSystemInstance'
        software_system_keyword='softwareSystem'
        stroke_keyword='stroke'
        strokewidth_keyword='strokeWidth'
        style_keyword='style'
        styles_keyword='styles'
        system_context_keyword='systemContext'
        system_landscape_keyword='systemLandscape'
        tags_keyword='tags'
        technology_keyword='technology'
        terminology_keyword='terminology'
        theme_keyword='theme'
        thickness_keyword='thickness'
        title_keyword='title'
        url_keyword='url'
        users_keyword='users'
        views_keyword='views'
        width_keyword='width'
        workspace_keyword='workspace'

        quoted_text='regexp:"[^"\r\n]*"'
        unquoted_text='regexp:[^\s"\r\n]+'

        identifier='regexp:[a-zA-Z_0-9]+'

        script_text='script_text'
    ]
}

structurizrDSLFile ::= statement*

private statement ::= elementDefinition | scriptBlock | explicitRelationship | implicitRelationship | identifierReferences

private elementDefinition ::= singleLineStatement | blockStatement | propertyBlockStatement | CRLF

singleLineStatement ::= identifierAssignment? keywordWithArguments lf_eof
blockStatement ::= identifierAssignment? keywordWithArguments '{' CRLF statement* '}' lf_eof

private identifierAssignment ::= identifierName '='

propertyBlockStatement ::= keywordWithPropertyBlock '{' CRLF keyValuePair* '}'
keyValuePair ::= key value CRLF
key ::= text
value ::= text

scriptBlock ::= '!script' argument* '{' scriptContents '}'
scriptContents ::= SCRIPT_TEXT+

explicitRelationship ::= identifierName '->' identifierName argument*
implicitRelationship ::= '->' identifierName argument*

identifierReferences ::= identifierName+ lf_eof

private keywordWithArguments ::= keyword argument*

keyword ::= '!adrs' | 'animation' | 'autoLayout' | 'background' | 'border' | 'color' | 'colour' | 'component' |
            '!constant' | 'containerInstance' | 'container' | 'custom' | 'deploymentEnvironment' | 'deploymentGroup' |
            'deployment' | 'deploymentNode' | 'description' | '!docs' | 'dynamic' | 'element' | 'enterprise' |
            'exclude' | 'extends' | 'filtered' | 'fontSize' | 'group' | 'healthCheck' | 'height' | 'icon' |
            '!identifiers' | '!impliedRelationships' | 'include' | '!include' | 'infrastructureNode' | 'instances' |
            'metadata' | 'model' | 'opacity' | 'person' | 'perspectives' | '!plugin' | 'position' | '!ref' | '->' |
            'routing' | '!script' | 'shape' | 'softwareSystemInstance' | 'softwareSystem' | 'stroke' | 'strokeWidth' |
            'style' | 'styles' | 'systemContext' | 'systemLandscape' | 'tags' | 'technology' | 'theme' | 'thickness' |
            'title' | 'url' | 'views' | 'width' | 'workspace'

keywordWithPropertyBlock ::= 'branding' | 'configuration' | 'properties' | 'terminology' | 'users'

argument ::= text
identifierName ::= IDENTIFIER

private text ::= QUOTED_TEXT | UNQUOTED_TEXT
private lf_eof ::= CRLF | <<eof>>
